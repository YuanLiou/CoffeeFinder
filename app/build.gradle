apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.akaita.android.easylauncher'
apply from: "${rootDir}/gradle/build-script.gradle"

android {
    compileSdkVersion compile_sdk_version
    defaultConfig {
        applicationId "tw.com.louis383.coffeefinder"
        targetSdkVersion target_sdk_version
        minSdkVersion min_sdk_version
        versionCode getVersionCodeTimeStamps()
        versionName app_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'HOST', APIHost
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(keystorePath)
            keyAlias keystoreAlias
            storePassword storePass
            keyPassword keyPass
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            manifestPlaceholders = [ googleMapKeys: googleMapKey_debug ]
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [ googleMapKeys: googleMapKey_release ]
        }
    }
}

easylauncher {
    iconNames "@mipmap/ic_launcher"
    foregroundIconNames "@mipmap/ic_launcher_foreground"

    buildTypes {
        debug {
            filters = [ orangeRibbonFilter("EXP") ]
        }
    }
}

androidExtensions {
    experimental = true
}

ext {
    retrofit_version = '2.1.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":anchor-bottom-sheet-behavior")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.core:core-ktx:1.0.0'

    implementation "com.google.android.gms:play-services-location:${gms_version}"
    implementation "com.google.android.gms:play-services-maps:${gms_version}"

    // HTTP Client
    implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit_version}"
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    implementation 'com.squareup.okhttp3:logging-interceptor:3.5.0'

    implementation 'io.reactivex.rxjava2:rxjava:2.0.8'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'

    implementation 'com.google.code.gson:gson:2.3.1'

    // Dependency Injection
    implementation 'com.google.dagger:dagger:2.9'
    kapt 'com.google.dagger:dagger-compiler:2.9'

    // ViewPager Indicator
    implementation 'com.romandanylyk:pageindicatorview:1.0.1@aar'

    // for unit test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
repositories {
    mavenCentral()
}
